export chebygrid, ischebygrid, chebycoef, cheby, chebyderiv
export ChebyshevInterpolator, BichebyshevInterpolator

Î¾2x(Î¾, a, b) = (Î¾ + 1)*((b - a)/2) + a

x2Î¾(x, a, b) = 2*(x - a)/(b - a) - 1

x2Î¸(x, a, b) = acos(x2Î¾(x, a, b))

"""
    chebygrid(n)

Create an array of `n` chebyshev nodes in [-1,1]
"""
@memoize chebygrid(n::Int) = cos.(Ï€*(n-1:-1:0)/(n-1))

"""
    chebygrid(xa, xb, n)

Create an array of `n` chebyshev nodes in [`xa`,`xb`]
"""
chebygrid(xa, xb, n::Int) = Î¾2x.(chebygrid(n), xa, xb)

"""
    chebygrid(xa, xb, nx, ya, yb, ny)

Create a two-dimensional grid of chebyshev nodes using `nx` points along the first axis, in [`xa`,`xb`], and `ny` points along the second axis, in [`ya`,`yb`].
"""
function chebygrid(xa, xb, nx::Int, ya, yb, ny::Int)
    X = chebygrid(xa, xb, nx) .* ones(ny)'
    Y = chebygrid(ya, yb, ny)' .* ones(nx)
    return X, Y
end

function ischebygrid(x)::Bool
    n = length(x)
    c = chebygrid(n)
    xa, xb = minimum(x), maximum(x)
    @inbounds for i âˆˆ eachindex(x)
        Î¾ = x2Î¾(x[i], xa, xb)
        r = abs((Î¾ - c[i])/c[i]) #relative difference
        r > 1e-3 && return false 
    end
    return true
end

@memoize function chebymatrix(n::Int)
    @assert n > 1 "can't construct cheby matrix smaller than 2 x 2"
    A = zeros(n,n)
    Î¾ = chebygrid(n)
    for j âˆˆ 1:n, k âˆˆ 1:n
        @inbounds A[j,k] = cos((k-1)*acos(Î¾[j]))
    end
    return A
end

function chebyrecurrance!(T, Î¾, L::Int)::Nothing
    Tâ‚–â‚‹â‚‚ = one(Î¾)
    Tâ‚–â‚‹â‚ = Î¾
    @inbounds T[2] = Tâ‚–â‚‹â‚
    for k = 3:L
        #compute next value
        Tâ‚– = 2Î¾*Tâ‚–â‚‹â‚ - Tâ‚–â‚‹â‚‚
        #set array value
        @inbounds T[k] = Tâ‚–
        #swaps
        Tâ‚–â‚‹â‚‚ = Tâ‚–â‚‹â‚
        Tâ‚–â‚‹â‚ = Tâ‚–
    end
    nothing
end

function chebyrecurrance(Î¾::U, L::Int) where {U}
    T = Vector{U}(undef, L)
    @inbounds T[1] = one(Î¾)
    chebyrecurrance!(T, Î¾, L)
    return T
end

"""
    chebycoef(y)

Compute the Chebyshev expansion coefficients for a set of points `y`, which are assumed to be located on the Chebyshev points for some interval.
"""
function chebycoef(y)
    n = length(y)
    @assert n > 1 "must have at least 2 points to form chebyshev coefficients"
    Aá´µ = invertedchebymatrix(n)
    return Aá´µ*y
end

"""
    cheby(coef, x, xa, xb)

Evaluates the Chebyshev expansion represented by the coefficients in `coef` and defined on the interval [`xa`,`xb`] at the point `x`.
"""
function cheby(coef, x, xa, xb)
    N = length(coef)
    @assert xa <= x <= xb "x must be inside [xa,xb]"
    #get coordinate in Î¾ space
    Î¾ = x2Î¾(x, xa, xb)
    #first two elements of cheby recursion
    Tâ‚–â‚‹â‚‚ = one(Î¾)
    Tâ‚–â‚‹â‚ = Î¾
    #first two terms of dot product
    @inbounds y = Tâ‚–â‚‹â‚‚*coef[1] + Tâ‚–â‚‹â‚*coef[2]
    #cheby recursion and rest of terms in dot product, all at once
    for k = 3:N
        #next value in recursion
        Tâ‚– = 2*Î¾*Tâ‚–â‚‹â‚ - Tâ‚–â‚‹â‚‚
        #next term in dot product
        @inbounds y += Tâ‚–*coef[k]
        #swaps
        Tâ‚–â‚‹â‚‚ = Tâ‚–â‚‹â‚
        Tâ‚–â‚‹â‚ = Tâ‚–
    end
    return y
end

"""
    chebyderiv(coef, xa, xb)

Generates the expansion coefficents for the derivative of a preexisting Chebyshev expansion defined on the interval [`xa`,`xb`].
"""
function chebyderiv(coef, xa, xb)
    n = length(coef)
    T = eltype(coef)
    @assert xa < xb "xa must be less than xb"
    #length 1 case is a trivial zero slope
    n == 1 && return zeros(T, 1)
    #start the array and check for length 2 case
    d = zeros(T, n-1)
    d[n-1] = 2*(n-2)*coef[n]
    n == 2 && return d
    #recurrance for all the rest
    d[n-2] = 2*(n-3)*coef[n-1]
    @inbounds for k âˆˆ n-2:-1:2
        d[k-1] = d[k+1] + 2*(k-1)*coef[k]
    end
    #interval width factor
    d .*= 2/(xb - xa)
    #extra DC scaling
    d[1] /= 2
    return d
end

#-------------------------------------------------------------------------------
# caching function for inverted cheby matrices, needed for interpolator setup

@memoize invertedchebymatrix(n::Int64)::Matrix{Float64} = inv(chebymatrix(n))

#-------------------------------------------------------------------------------
# one-dimensional interpolation

struct ChebyshevInterpolator{N,T}
    #lowest value in range
    xa::T
    #highest value in range
    xb::T
    #interpolation coefficents
    a::NTuple{N,T}
    #must always have strict boundaries
    boundaries::StrictBoundaries
end

function Base.show(io::IO, Ï•::ChebyshevInterpolator)
    print(io, "$(typeof(Ï•)) âˆˆ [$(Ï•.xa), $(Ï•.xb)]")
end

"""
    ChebyshevInterpolator(x, y)

Construct a `ChebyshevInterpolator` for the points defined by coordinates `x` and values `y`. The `x` coordinates *must* be arranged on a chebyshev grid, which can be generated using the [`chebygrid`](@ref) function.
"""
function ChebyshevInterpolator(x, y)
    #same types
    T = promote_type(eltype(x), eltype(y))
    x = collect(T, x)
    y = collect(T, y)
    #check for basic issues
    rangecheck(x, y, 3)
    #demand that the input points have chebyshev spacing
    @assert ischebygrid(x) "points must be on a chebyshev grid"
    #generate expansion coefficients
    a = Tuple(chebycoef(y))
    #construct
    ChebyshevInterpolator(minimum(x), maximum(x), a, StrictBoundaries())
end

"""
    ChebyshevInterpolator(f, xa, xb, n)

Construct a `ChebyshevInterpolator` for the function `f` using `n` function evaluations in the range [`xa`,`xb`]. The function evaluations will occur on the chebyshev nodes.
"""
function ChebyshevInterpolator(f::F, xa, xb, n::Int) where {F}
    #set up the range coordinates
    x = chebygrid(xa, xb, n)
    #evaluate the function at those coordinates
    y = f.(x)
    #call the other constructor
    ChebyshevInterpolator(x, y)
end

function (Ï•::ChebyshevInterpolator{N,U})(x) where {N,U}
    #always enforce boundaries
    Ï•.boundaries(x, Ï•.xa, Ï•.xb)
    #evaluate the expansion
    return cheby(Ï•.a, x, Ï•.xa, Ï•.xb)
end

"""
    chebyderiv(Ï•::ChebyshevInterpolator)

Construct a ChebyshevInterpolator representing the derivative of a preexisting interpolator.
"""
function chebyderiv(Ï•::ChebyshevInterpolator)
    #derivative expansion's coefficients
    coef = Tuple(chebyderiv(Ï•.a, Ï•.xa, Ï•.xb))
    #new interpolator
    ChebyshevInterpolator(Ï•.xa, Ï•.xb, coef, StrictBoundaries())
end

#-------------------------------------------------------------------------------
# bichebyshev interpolation, a little trickier now!

struct BichebyshevInterpolator{M,N,U}
    #lowest value on axis 1
    xa::U
    #highest value on axis 1
    xb::U
    #lowest value on axis 2
    ya::U
    #highest value on axis 2
    yb::U
    #matrix and vectors for doing the interpolation
    A::Matrix{U} # size (ny by nx) or (M by N)
    a::Vector{U} # length ny for cosine expansion in Î¸y
    b::Vector{U} # length nx for cosine expansion in Î¸x
    c::Vector{U} # length ny for doing M*b in place
    #must always use strict boundaries
    boundaries::StrictBoundaries
end

function Base.show(io::IO, Ï•::BichebyshevInterpolator)
    print(io, "$(typeof(Ï•)) âˆˆ [$(Ï•.xa), $(Ï•.xb)], [$(Ï•.ya), $(Ï•.yb)]")
end

"""
    BichebyshevInterpolator(x, y, Z)

Construct a `BichebyshevInterpolator` for the grid of points defined by coordinates (`x`,`y`) and values `Z`. The given points must lie on a chebyshev grid in each direction. These can be generated with the [`chebygrid`](@ref) function or the interpolator can be constructed directly from a function using the method below.

!!! warning

    The Bichebyshev interpolator is *not thread-safe*. It computes a cosine expansion and does some linear algebra in-place using arrays stored with the object. A single `BichebyshevInterpolator` should never be called by multiple threads at once.
"""
function BichebyshevInterpolator(x, y, Z)
    #same types
    T = promote_type(eltype(x), eltype(y), eltype(Z))
    x = collect(T, x)
    y = collect(T, y)
    Z = collect(T, Z)
    #check for basic grid problems
    gridcheck(x, y, Z, 3)
    #grid properties
    nx, ny = length(x), length(y)
    xa, xb = T(minimum(x)), T(maximum(x))
    ya, yb = T(minimum(y)), T(maximum(y))
    #reject any non-cheby grid spacing
    @assert ischebygrid(x) "axis 1 coordinates must be on a chebyshev grid"
    @assert ischebygrid(y) "axis 2 coordinates must be on a chebyshev grid"
    #get inverted matrices from cache or generate them
    B = invertedchebymatrix(nx)
    #generate interpolation coefficients along axis 1 for each value of axis 2
    Î± = zeros(T, nx, ny)
    for j = 1:ny
        mul!(view(Î±,:,j), B, view(Z,:,j))
    end
    #then combine Î± and A
    A = invertedchebymatrix(ny)*Î±'
    #other vectors we need for doing the actual interpolation
    a = ones(T, ny)
    b = ones(T, nx)
    c = zeros(T, ny)
    #done
    BichebyshevInterpolator{ny,nx,T}(xa, xb, ya, yb, A, a, b, c, StrictBoundaries())
end

"""
    BichebyshevInterpolator(f, xa, xb, nx, ya, yb, ny)

Construct a `BichebyshevInterpolator` for the function `f` using a grid of `nx` points on the first axis in [`xa`,`xb`] and `ny` points on the second axis in [`ya`,`yb`].
"""
function BichebyshevInterpolator(f::F, xa, xb, nx::Int, ya, yb, ny::Int) where {F}
    #set up the grid
    X, Y = chebygrid(xa, xb, nx, ya, yb, ny)
    #evaluate the function at chebyshev grid points
    Z = f.(X, Y)
    #call the other constructor
    BichebyshevInterpolator(X[:,1], Y[1,:], Z)
end

#=====
This is the fast implementation. It's executed when the types of the
input coordinates match the type of the stored coefficients in the
interpolator. When the types match, the Chebyshev expansions can be
evaluated in-place, using vectors pre-allocated in the interpolator.
See the a, b, and c fields of the struct. This method also guarantees
that the interpolator type and the coordinate types are <: AbstractFloat,
making the low-level linear algebra functions safe. Without such a
guarantee, there can be issues with, for example, the dual numbers in
FowardDiff routines.
=====#
function (Î¦::BichebyshevInterpolator{M,N,U})(x::U, y::U) where {M,N,U<:AbstractFloat}
    #always enforce boundaries for Chebyshev
    Î¦.boundaries(x, Î¦.xa, Î¦.xb, y, Î¦.ya, Î¦.yb)
    #evaluate Chebyshev polys at the coordinates recursively and in-place
    Î¾y = x2Î¾(y, Î¦.ya, Î¦.yb)
    chebyrecurrance!(Î¦.a, Î¾y, M)
    Î¾x = x2Î¾(x, Î¦.xa, Î¦.xb)
    chebyrecurrance!(Î¦.b, Î¾x, N)
    #perform M*b, which interpolates along the first axis, also in-place
    mul!(Î¦.c, Î¦.A, Î¦.b)
    #then a'*c interpolates along the second axis
    return dot(Î¦.a, Î¦.c)
end

#=====
This is the slow, but type-flexible, implementation. It's
executed whenever the type of the interpolator's coefficients or
the coordinate types don't match OR are not AbstractFloats. The
price of this flexibility is allocations for the expansions and
loss of the low-level linear algebra routines ðŸ˜­.
=====#
function (Î¦::BichebyshevInterpolator{M,N,U})(x, y) where {M,N,U}
    #always enforce boundaries for Chebyshev
    Î¦.boundaries(x, Î¦.xa, Î¦.xb, y, Î¦.ya, Î¦.yb)
    #coordinates in Î¾ space
    Î¾x, Î¾y = promote(x2Î¾(x, Î¦.xa, Î¦.xb), x2Î¾(y, Î¦.ya, Î¦.yb))
    #allocating expansion in the x direction
    b = chebyrecurrance(Î¾x, N)
    c = Î¦.A * b
    #then perform the y-axis expansion and dot product simulataneously    
    Tâ‚–â‚‹â‚‚ = one(Î¾y)
    Tâ‚–â‚‹â‚ = Î¾y
    @inbounds z = c[1] + Î¾y*c[2]
    for i = 3:M
        #compute next value
        Tâ‚– = 2Î¾y*Tâ‚–â‚‹â‚ - Tâ‚–â‚‹â‚‚
        #update running dot product
        @inbounds z += c[i]*Tâ‚–
        #swaps
        Tâ‚–â‚‹â‚‚ = Tâ‚–â‚‹â‚
        Tâ‚–â‚‹â‚ = Tâ‚–
    end
    return z
end