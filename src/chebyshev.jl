export chebygrid, ChebyshevInterpolator, BichebyshevInterpolator

Œæ2x(Œæ, a, b) = (Œæ + 1)*((b - a)/2) + a

x2Œæ(x, a, b) = 2*(x - a)/(b - a) - 1

x2Œ∏(x, a, b) = acos(x2Œæ(x, a, b))

"""
    chebygrid(n)

Create an array of `n` chebyshev nodes in [-1,1]
"""
chebygrid(n::Int) = cos.(œÄ*(n-1:-1:0)/(n-1))

"""
    chebygrid(xa, xb, n)

Create an array of `n` chebyshev nodes in [`xa`,`xb`]
"""
chebygrid(xa, xb, n::Int) = Œæ2x.(chebygrid(n), xa, xb)

"""
    chebygrid(xa, xb, nx, ya, yb, ny)

Create a two-dimensional grid of chebyshev nodes using `nx` points along the first axis, in [`xa`,`xb`], and `ny` points along the second axis, in [`ya`,`yb`].
"""
function chebygrid(xa, xb, nx::Int, ya, yb, ny::Int)
    X = chebygrid(xa, xb, nx) .* ones(ny)'
    Y = chebygrid(ya, yb, ny)' .* ones(nx)
    return X, Y
end

function ischebygrid(x)::Bool
    all(x2Œæ.(x, minimum(x), maximum(x)) .- chebygrid(length(x)) .< 1e-6)
end

#could be slower than the recurrance
cheby(Œæ, k::Int) = cos(k*acos(Œæ))

function chebymatrix(n::Int)
    @assert n > 1 "can't construct cheby matrix smaller than 2 x 2"
    A = zeros(n,n)
    Œæ = chebygrid(n)
    for j ‚àà 1:n, k ‚àà 1:n
        @inbounds A[j,k] = cheby(Œæ[j], k-1)
    end
    return A
end

function chebyrecurrance!(T, Œæ, L::Int)::Nothing
    T‚Çñ‚Çã‚ÇÇ = one(Œæ)
    T‚Çñ‚Çã‚ÇÅ = Œæ
    @inbounds T[2] = T‚Çñ‚Çã‚ÇÅ
    for k = 3:L
        #compute next value
        T‚Çñ = 2Œæ*T‚Çñ‚Çã‚ÇÅ - T‚Çñ‚Çã‚ÇÇ
        #set array value
        @inbounds T[k] = T‚Çñ
        #swaps
        T‚Çñ‚Çã‚ÇÇ = T‚Çñ‚Çã‚ÇÅ
        T‚Çñ‚Çã‚ÇÅ = T‚Çñ
    end
    nothing
end

function chebyrecurrance(Œæ::U, L::Int) where {U}
    T = Vector{U}(undef, L)
    @inbounds T[1] = one(Œæ)
    chebyrecurrance!(T, Œæ, L)
    return T
end

#-------------------------------------------------------------------------------
# caching function for inverted cheby matrices, needed for interpolator setup

@memoize function invertedchebymatrix(n::Int64)::Matrix{Float64}
    inv(chebymatrix(n))
end

#-------------------------------------------------------------------------------
# one-dimensional interpolation

struct ChebyshevInterpolator{N,T}
    #lowest value in range
    xa::T
    #highest value in range
    xb::T
    #interpolation coefficents
    a::NTuple{N,T}
    #must always have strict boundaries
    boundaries::StrictBoundaries
end

"""
    ChebyshevInterpolator(x, y)

Construct a `ChebyshevInterpolator` for the points defined by coordinates `x` and values `y`. The `x` coordinates *must* be arranged on a chebyshev grid, which can be generated using the [`chebygrid`](@ref) function.
"""
function ChebyshevInterpolator(x, y)
    #same types
    T = promote_type(eltype(x), eltype(y))
    x = collect(T, x)
    y = collect(T, y)
    #check for basic issues
    rangecheck(x, y, 3)
    #demand that the input points have chebyshev spacing
    @assert ischebygrid(x) "points must be on a chebyshev grid"
    #get the inverted cheby matrix, from cache or fresh
    N = length(x)
    A = invertedchebymatrix(N)
    #generate expansion coefficients
    a = Tuple(A*y)
    #construct
    ChebyshevInterpolator{N,T}(minimum(x), maximum(x), a, StrictBoundaries())
end

"""
    ChebyshevInterpolator(f, xa, xb, n)

Construct a `ChebyshevInterpolator` for the function `f` using `n` function evaluations in the range [`xa`,`xb`]. The function evaluations will occur on the chebyshev nodes.
"""
function ChebyshevInterpolator(f::F, xa, xb, n::Int) where {F}
    #set up the range coordinates
    x = chebygrid(xa, xb, n)
    #evaluate the function at those coordinates
    y = f.(x)
    #call the other constructor
    ChebyshevInterpolator(x, y)
end

function (œï::ChebyshevInterpolator{N,U})(x) where {N,U}
    #always enforce boundaries
    œï.boundaries(x, œï.xa, œï.xb)
    #get coordiante in Œæ space
    Œæ = x2Œæ(x, œï.xa, œï.xb)
    #first two elements of cheby recursion
    T‚Çñ‚Çã‚ÇÇ = one(Œæ)
    T‚Çñ‚Çã‚ÇÅ = Œæ
    #first two terms of dot product
    @inbounds y = T‚Çñ‚Çã‚ÇÇ*œï.a[1] + T‚Çñ‚Çã‚ÇÅ*œï.a[2]
    #cheby recursion and rest of terms in dot product, all at once
    for k = 3:N
        #next value in recursion
        T‚Çñ = 2*Œæ*T‚Çñ‚Çã‚ÇÅ - T‚Çñ‚Çã‚ÇÇ
        #next term in dot product
        @inbounds y += T‚Çñ*œï.a[k]
        #swaps
        T‚Çñ‚Çã‚ÇÇ = T‚Çñ‚Çã‚ÇÅ
        T‚Çñ‚Çã‚ÇÅ = T‚Çñ
    end
    return y
end

#-------------------------------------------------------------------------------
# bichebyshev interpolation, a little trickier now!

struct BichebyshevInterpolator{M,N,U}
    #lowest value on axis 1
    xa::U
    #highest value on axis 1
    xb::U
    #lowest value on axis 2
    ya::U
    #highest value on axis 2
    yb::U
    #matrix and vectors for doing the interpolation
    A::Matrix{U} # size (ny by nx) or (M by N)
    a::Vector{U} # length ny for cosine expansion in Œ∏y
    b::Vector{U} # length nx for cosine expansion in Œ∏x
    c::Vector{U} # length ny for doing M*b in place
    #must always use strict boundaries
    boundaries::StrictBoundaries
end

"""
    BichebyshevInterpolator(x, y, Z)

Construct a `BichebyshevInterpolator` for the grid of points defined by coordinates (`x`,`y`) and values `Z`. The given points must lie on a chebyshev grid in each direction. These can be generated with the [`chebygrid`](@ref) function or the interpolator can be constructed directly from a function using the method below.

!!! warning

    The Bichebyshev interpolator is *not thread-safe*. It computes a cosine expansion and does some linear algebra in-place using arrays stored with the object. A single `BichebyshevInterpolator` should never be called by multiple threads at once.
"""
function BichebyshevInterpolator(x, y, Z)
    #same types
    T = promote_type(eltype(x), eltype(y), eltype(Z))
    x = collect(T, x)
    y = collect(T, y)
    Z = collect(T, Z)
    #check for basic grid problems
    gridcheck(x, y, Z, 3)
    #grid properties
    nx, ny = length(x), length(y)
    xa, xb = T(minimum(x)), T(maximum(x))
    ya, yb = T(minimum(y)), T(maximum(y))
    #reject any non-cheby grid spacing
    @assert ischebygrid(x) "axis 1 coordinates must be on a chebyshev grid"
    @assert ischebygrid(y) "axis 2 coordinates must be on a chebyshev grid"
    #get inverted matrices from cache or generate them
    B = invertedchebymatrix(nx)
    #generate interpolation coefficients along axis 1 for each value of axis 2
    Œ± = zeros(T, nx, ny)
    for j = 1:ny
        mul!(view(Œ±,:,j), B, view(Z,:,j))
    end
    #then combine Œ± and A
    A = invertedchebymatrix(ny)*Œ±'
    #other vectors we need for doing the actual interpolation
    a = ones(T, ny)
    b = ones(T, nx)
    c = zeros(T, ny)
    #done
    BichebyshevInterpolator{ny,nx,T}(xa, xb, ya, yb, A, a, b, c, StrictBoundaries())
end

"""
    BichebyshevInterpolator(f, xa, xb, nx, ya, yb, ny)

Construct a `BichebyshevInterpolator` for the function `f` using a grid of `nx` points on the first axis in [`xa`,`xb`] and `ny` points on the second axis in [`ya`,`yb`].
"""
function BichebyshevInterpolator(f::F, xa, xb, nx::Int, ya, yb, ny::Int) where {F}
    #set up the grid
    X, Y = chebygrid(xa, xb, nx, ya, yb, ny)
    #evaluate the function at chebyshev grid points
    Z = f.(X, Y)
    #call the other constructor
    BichebyshevInterpolator(X[:,1], Y[1,:], Z)
end

#=====
This is the fast implementation. It's executed when the types of the
input coordinates match the type of the stored coefficients in the
interpolator. When the types match, the Chebyshev expansions can be
evaluated in-place, using vectors pre-allocated in the interpolator.
See the a, b, and c fields of the struct. This method also guarantees
that the interpolator type and the coordinate types are <: AbstractFloat,
making the low-level linear algebra functions safe. Without such a
guarantee, there can be issues with, for example, the dual numbers in
FowardDiff routines.
=====#
function (Œ¶::BichebyshevInterpolator{M,N,U})(x::U, y::U) where {M,N,U<:AbstractFloat}
    #always enforce boundaries for Chebyshev
    Œ¶.boundaries(x, Œ¶.xa, Œ¶.xb, y, Œ¶.ya, Œ¶.yb)
    #evaluate Chebyshev polys at the coordinates recursively and in-place
    Œæy = x2Œæ(y, Œ¶.ya, Œ¶.yb)
    chebyrecurrance!(Œ¶.a, Œæy, M)
    Œæx = x2Œæ(x, Œ¶.xa, Œ¶.xb)
    chebyrecurrance!(Œ¶.b, Œæx, N)
    #perform M*b, which interpolates along the first axis, also in-place
    mul!(Œ¶.c, Œ¶.A, Œ¶.b)
    #then a'*c interpolates along the second axis
    return dot(Œ¶.a, Œ¶.c)
end

#=====
This is the slow, buttype-flexible, implementation. It's
executed whenever the type of the interpolator's coefficients or
the coordinate types don't match OR are not AbstractFloats. The
price of this flexibility is allocations for the expansions and
loss of the low-level linear algebra routines üò≠.
=====#
function (Œ¶::BichebyshevInterpolator{M,N,U})(x, y) where {M,N,U}
    #always enforce boundaries for Chebyshev
    Œ¶.boundaries(x, Œ¶.xa, Œ¶.xb, y, Œ¶.ya, Œ¶.yb)
    #coordinates in Œæ space
    Œæx, Œæy = promote(x2Œæ(x, Œ¶.xa, Œ¶.xb), x2Œæ(y, Œ¶.ya, Œ¶.yb))
    #allocating expansion in the x direction
    b = chebyrecurrance(Œæx, N)
    c = Œ¶.A * b
    #then perform the y-axis expansion and dot product simulataneously    
    T‚Çñ‚Çã‚ÇÇ = one(Œæy)
    T‚Çñ‚Çã‚ÇÅ = Œæy
    @inbounds z = c[1] + Œæy*c[2]
    for i = 3:M
        #compute next value
        T‚Çñ = 2Œæy*T‚Çñ‚Çã‚ÇÅ - T‚Çñ‚Çã‚ÇÇ
        #update running dot product
        @inbounds z += c[i]*T‚Çñ
        #swaps
        T‚Çñ‚Çã‚ÇÇ = T‚Çñ‚Çã‚ÇÅ
        T‚Çñ‚Çã‚ÇÅ = T‚Çñ
    end
    return z
end